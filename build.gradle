apply plugin: 'scala'

ext {
  scalaMajorVersion = '2.11'
  scalaVersion = scalaMajorVersion+'.6'
  akkaVersion = '2.3.11'
  configVersion = '1.3.0'
  ESVersion = '1.6.0'
  LuceneVersion = '4.10.4'
}

repositories {
  mavenLocal()
  mavenCentral()
}

configurations {
  elasticsearch
  mongo
}

dependencies {
  compile "org.scala-lang:scala-library:$scalaVersion",
          "com.typesafe.akka:akka-actor_$scalaMajorVersion:$akkaVersion",
          "com.typesafe.akka:akka-slf4j_$scalaMajorVersion:$akkaVersion",
          "com.typesafe:config:$configVersion",
          configurations.elasticsearch,
          configurations.mongo

  elasticsearch "org.elasticsearch:elasticsearch:$ESVersion",
                "org.apache.lucene:lucene-core:$LuceneVersion"

  mongo 'org.mongodb:mongo-java-driver:2.12.5',
        'org.jongo:jongo:1.1',
        'de.undercouch:bson4jackson:2.4.0'
              
} 

task run(type: JavaExec, dependsOn: 'build') {
  classpath configurations.compile, "$buildDir/classes/main"
  main 'ap.loader.akka.Main'
  systemProperty 'nbPar', project.hasProperty('nbPar') ? project.getProperty('nbPar') : '1'
  systemProperty 'nbMsg', project.hasProperty('nbMsg') ? project.getProperty('nbMsg') : '1'
  systemProperty 'payloadSize', project.hasProperty('payloadSize') ? project.getProperty('payloadSize') : '1'
}

